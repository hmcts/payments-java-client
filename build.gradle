plugins {
    id 'checkstyle'
    id 'io.spring.dependency-management' version '1.0.9.RELEASE'
    id 'org.springframework.boot' version '2.2.5.RELEASE'
    id 'com.github.ben-manes.versions' version '0.28.0'
    id 'com.jfrog.bintray' version '1.8.5'
    id 'java-library'
    id 'maven'
    id 'maven-publish'
    id 'net.ltgt.apt' version '0.21'
}

// This will pick up the version number from release tag
def buildNumber = System.getenv("RELEASE_VERSION")?.replace("refs/tags/", "") ?: "DEV-SNAPSHOT"

group 'uk.gov.hmcts.reform'
version buildNumber

dependencyUpdates.resolutionStrategy = {
    componentSelection { rules ->
        rules.all { ComponentSelection selection ->
            boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm'].any { qualifier ->
                selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
            }
            if (rejected) {
                selection.reject('Release candidate')
            }
        }
    }
}

def springBootVersion = '2.5.4'

checkstyle {
    maxWarnings = 0
    toolVersion = '8.29'
    // need to set configDir to rootDir otherwise submodule will use submodule/config/checkstyle
    configDir = new File(rootDir, 'config/checkstyle')
}

sourceCompatibility = 11

compileJava {
    options.compilerArgs << '-parameters' << '-Xlint:deprecation'
}

compileTestJava {
    options.compilerArgs << '-Xlint:deprecation'
}

repositories {
    mavenLocal()
    jcenter()
    maven {
        url 'https://jitpack.io'
    }
}

jar {
    manifest {
        attributes 'Implementation-Title': project.name, 'Implementation-Version': project.version
    }
}

task printVersion {
    doLast {
        print project.version
    }
}

def gitRepo = "https://github.com/hmcts/payments-java-client"

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

test {
    useJUnitPlatform()
}

ext {
    lombokVersion = '1.18.28'
    springCloudVersion = 'Hoxton.SR2'
    feignVersion = '10.3.0'
}

def versions = [
        logback          : '1.2.3',
        springBoot       : springBootVersion,
        reformJavaLogging: '5.0.1',
        junitJupiter     : '5.10.0'
]

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

sourceSets {
    functionalTest {
        java {
            compileClasspath += main.output
            runtimeClasspath += main.output
            srcDir('src/functionalTest/java')
        }
        resources {
            srcDir('src/functionalTest/resources')
        }
    }
}

configurations {
    functionalTestImplementation.extendsFrom testImplementation
    functionalTestRuntimeOnly.extendsFrom testRuntimeOnly
}

task functionalTest(type: Test) {
    description = 'Runs functional tests.'
    group = 'verification'

    testClassesDirs = sourceSets.functionalTest.output.classesDirs
    classpath = sourceSets.functionalTest.runtimeClasspath
    include "uk/gov/hmcts/reform/payments/client/functional/**"

    useJUnitPlatform()
}

dependencies {
    api group: 'org.springframework.cloud', name: 'spring-cloud-starter-openfeign'
    api group: 'io.github.openfeign', name: 'feign-jackson', version: feignVersion

    compileOnly group: 'org.projectlombok', name: 'lombok', version: lombokVersion
    annotationProcessor group: 'org.projectlombok', name: 'lombok', version: lombokVersion

    compile group: 'ch.qos.logback', name: 'logback-classic', version: versions.logback
    compile group: 'ch.qos.logback', name: 'logback-core', version: versions.logback
    compile group: 'uk.gov.hmcts.reform', name: 'java-logging', version: versions.reformJavaLogging

    compile group: 'uk.gov.hmcts.reform', name: 'service-auth-provider-client', version: '3.1.4'

    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter', version: versions.junitJupiter
    testImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-test'

    testImplementation 'org.mockito:mockito-junit-jupiter:2.23.0'
    testImplementation 'ru.lanwen.wiremock:wiremock-junit5:1.3.1'

    testImplementation group: 'com.github.tomakehurst', name: 'wiremock', version: '2.33.2'
    testImplementation group: 'org.springframework.cloud', name: 'spring-cloud-contract-wiremock'

    functionalTestImplementation group: 'uk.gov.hmcts.reform', name: 'service-auth-provider-client', version: '3.1.4'
    functionalTestCompileOnly group: 'org.projectlombok', name: 'lombok', version: lombokVersion
    functionalTestAnnotationProcessor group: 'org.projectlombok', name: 'lombok', version: lombokVersion
}

def pomConfig = {
    licenses {
        license {
            name "MIT License"
            url "http://www.opensource.org/licenses/mit-license.php"
            distribution "repo"
        }
    }

    scm {
        url "https://github.com/hmcts/payments-java-client"
    }
}

publishing {
    publications {
        Main(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            groupId project.group
            artifactId 'payments-client'
            version project.version

            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'Client library for communicating with payments api')
                root.appendNode('name', 'Payments client')
                root.appendNode('url', gitRepo)
                root.children().last() + pomConfig
            }
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publications = ['Main']
    publish = true
    pkg {
        repo = 'hmcts-maven'
        name = 'payments-client'
        userOrg = 'hmcts'
        licenses = ['MIT']
        vcsUrl = gitRepo
        version {
            name = project.version
        }
    }
}

wrapper {
    distributionType = Wrapper.DistributionType.ALL
}

bootJar {
    enabled = false
}

jar {
    enabled = true
}
